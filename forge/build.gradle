import net.minecraftforge.gradle.common.task.SignJar

buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}

apply plugin: "net.minecraftforge.gradle"

minecraft {
    mappings channel: "$mappings_channel", version: "$mappings_version"
    runs {
        client {
            workingDirectory project.file("../run")
            property "forge.logging.console.level", "debug"
            mods {
                ibeeditor {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('../run')
            property "forge.logging.console.level", "debug"
            mods {
                ibeeditor {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:$forge_version"
    implementation project(":common")
}

processResources {
    from "src/main/resources", "../common/src/main/resources"
}

task signJar(type: SignJar) {
    onlyIf {
        project.hasProperty("keyStore")
    }

    keyStore = project.keyStore
    alias = project.keyStoreAlias
    storePass = project.keyStorePass
    keyPass = project.keyStoreKeyPass
    inputFile = rootProject.jar.archivePath
    outputFile = rootProject.jar.archivePath
}